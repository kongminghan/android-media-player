apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

def configPropertiesFile = rootProject.file("config.properties")
def configProperties = new Properties()
configProperties.load(new FileInputStream(configPropertiesFile))

android {
    signingConfigs {
        release {
            keyAlias configProperties['key_alias']
            keyPassword configProperties['key_password']
            storeFile rootProject.file("lomotif.keystore")
            storePassword configProperties['keystore_password']
        }
    }
    compileSdkVersion 28
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "com.minghan.lomotif.media"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 3
        versionName "0.1.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        it.each {
            it.buildConfigField "String", "PIXABAY_URL", "\"https://pixabay.com/\""
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    def nav_version = "2.1.0-rc01"
    def paging_version = "2.1.0"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.core:core-ktx:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.1.0-alpha09'

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Dagger
    implementation 'com.google.dagger:dagger:2.22.1'
    implementation 'com.google.dagger:dagger-android-support:2.22.1'
    kapt 'com.google.dagger:dagger-android-processor:2.22.1'
    kapt 'com.google.dagger:dagger-compiler:2.22.1'

    // Viewmodel
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'

    // Permission
    implementation 'com.nabinbhandari.android:permissions:3.8'

    // Fresco for image loading
    implementation 'com.facebook.fresco:fresco:2.0.0'
    implementation 'com.facebook.fresco:webpsupport:2.0.0'

    // Image zooming
    implementation 'me.relex:photodraweeview:2.0.0'

    // ExoPlayer for playing music and video
    implementation 'com.google.android.exoplayer:exoplayer:2.10.1'
    implementation ("com.github.PaulWoitaschek.ExoPlayer-Extensions:extension-flac:2.10.2") {
        transitive = false
    }

    // Audio visualizer
    implementation 'com.gauravk.audiovisualizer:audiovisualizer:0.9.2'

    // Paging component
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.6.1'
    implementation 'com.squareup.retrofit2:converter-moshi:2.6.1'

    // Downloader
    implementation "androidx.tonyodev.fetch2:xfetch2:3.1.4"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
